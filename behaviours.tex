\chapter{Finding discrepancies in behaviour usage}
\label{chp:behaviours}

As described in Section~\ref{sct:behaviour_preliminaries}, behaviours
are \er's equivalent of \emph{abstract classes}, as they appear in
object-oriented languages like Java. When using \emph{abstract
  classes} the developer might make trivial mistakes such as
forgetting to implement a particular abstract method or implementing
it incorrectly so that it ``doesn't fit'' with those already
provided. In \er\ the abstract methods are called
\emph{callbacks}. \er's compiler detects only the lack of
implementation of any \emph{callbacks} but \dr\ can be used to further
aid the developer by verifying whether his implementations have the
expected success typings, ensuring thus that they ``fit well'' with
the already provided infrastructure. \dr's recent feature, race
analysis~\cite{Races@PADL-10}, can also be extended to detect races
present in code that uses behaviours. Each extension will be presented
in a separate section.

\section{Usage of behaviours}

\er\ developers use behaviours heavily as they readily provide some of
the key features of the language (concurrency, communication,
distribution and fault-tolerance) and allow the developer to focus on
the particular aspects of his implementation, ignoring these
parameters. In Table~\ref{tab:otp_behaviours} the most common
behaviours in OTP are presented, along with some of the callbacks they
require.

\input{behaviours_table}

Developers may also write their own behaviours, whenever a common
infrastructure may be used for many specific implementations.

\subsection{Declaration of a behaviour}

A module describing a behaviour exports a specific function:
\texttt{behaviour\_info(callbacks)}. This returns the expected
callbacks in the form of a list of tuples containing the names of the
callback functions as atoms and their arity as integers. The example
in Listing~\ref{lst:behaviour_info} is taken from the
\genserv\ behaviour.

\begin{console}{lst:behaviour_info}{Generic server's declaration of callbacks}
-export([behaviour_info/1]).

behaviour_info(callbacks) ->
  [{init,1}, {handle_call,3}, {handle_cast,2},
   {terminate, 2}, {code_change, 3}].

%%% The user module should export:
%%%
%%%   init(Args)
%%%     ==> {ok, State}
%%%         {ok, State, Timeout}
%%%         ignore
%%%         {stop, Reason}
%%%
%%%   handle_call(Msg, {From, Tag}, State)
%%%
%%%    ==> {reply, Reply, State}
%%%        {reply, Reply, State, Timeout}
%%%        {noreply, State}
%%%        {noreply, State, Timeout}
%%%        {stop, Reason, Reply, State}
%%%              Reason = normal | shutdown | Term
%%%
%%% ....  MORE COMMENTS FOR THE OTHER THREE CALLBACKS HERE .....

\end{console}

\subsection{Better declaration of a behaviour}

Often, though not always, the behaviour module also contains some
additional information in the form of comments, as shown in
Listing~\ref{lst:behaviour_info}. The problem with comments is that
they are in free text form, often lacking some information as in the
case above, and cannot be trusted or mechanically processed.

Instead of the form described in the previous Listing, the
\texttt{behaviour\_info(callbacks)} clause can be substituted with the
attributes shown in Listing~\ref{lst:callback_attributes} which also
specify the types which are expected from these callbacks.

\begin{console}{lst:callback_attributes}{Callback attributes}
-callback init(Args :: term()) ->
    {ok, State :: term()} |
    {ok, State :: term(), timeout() | hibernate} |
    {stop, Reason :: term()} | 
    ignore.

-callback handle_call(Request :: term(), From :: {pid(), Tag :: term()},
                      State :: term()) ->
    {reply, Reply :: term(), State :: term()} |
    {reply, Reply :: term(), State :: term(), timeout() | hibernate} |
    {noreply, State :: term()} |
    {noreply, State :: term(), timeout() | hibernate} |
    {stop, Reason :: normal | shutdown | term(), Reply :: term(), 
                                                 State :: term()} |
    {stop, Reason :: term(), State :: term()}.

%%% .... MORE CALLBACK ATTRIBUTES .... %%%

\end{console}

These attributes are identical with specs so \dr\ can use these as a
reference to compare the inferred types of the callbacks.
Incidentally, the above example shows various interesting things:

\begin{enumerate}
\item Using the language of types and specs, one can provide
  information both for documentation purposes and for types as e.g. in
  \texttt{From :: {pid(), Tag :: term()}}
\item Comments are often incomplete or can easily become obsolete as
  e.g. the \texttt{hibernate} value is nowhere mentioned.
\end{enumerate}

\section{Finding discrepancies in callbacks}
\label{sct:behaviour_discrepancies}

\dr's extension to use these callback attributes was simple and
straightforward. After the success typings were calculated, they were
compared against the attributes and warnings were emitted when the
latter were not subtypes of the former. In this way the discrepancies
shown in Table~\ref{tab:erl_apps_behaviours} were found \footnote{Only
  definite results are presented. Some more results need verification
  from the OTP team as the documentation on which the callback
  attributes were based might be outdated.}

\input{behaviours_results}

All these discrepancies correspond to cases where a callback has a
wider return type than the one described in the relevant
attribute. The most common warning was about the return value of
\genserv's callbaks \texttt{handle\_cast} and \texttt{handle\_info}
which sometimes erroneously included \texttt{\{reply, ...\}}.

Some special attention was given to the \genserv\ module, as its API
returns depended also on the success typings of the
callbacks. Specifically for the API's \texttt{start} and
\texttt{start\_link} functions, \emph{``If callback init/1 fails with
  Reason, the function returns \{error,Reason\}. If callback init/1
  returns \{stop,Reason\} or ignore, the process is terminated and the
  function returns \{error,Reason\} or ignore, respectively.''}. This
was taken into account when calls to these functions were found, as
otherwise false warnings were emitted for the inets and other
applications.

\dr\ emitted some other false warnings as well. These warnings came
from situations where two or more callback functions used the result
of a common underlying function as a reply, or when one callback
called directly another. The example presented in
Listing~\ref{lst:mnesia_false_warning}, from the mnesia application,
falls into the latter category, as \texttt{handle\_info} calls a
specific clause of \texttt{handle\_call}. Using the existing algorithm
for type inference this will result in the inclusion of \emph{all} of
\texttt{handle\_call}'s possible returns in \texttt{handle\_info}'s
return type. This causes the warning shown in the end of the Listing
to be emitted, as \texttt{handle\_call} might also return
\texttt{\{reply,\ldots\}} in other clauses, even though this will
never happen in the call from \texttt{handle\_info} (the returns of
which are in lines 8, 11 and 23 and are all
\texttt{\{noreply,\ldots\}}).

\begin{code}{lst:mnesia_false_warning}{A false warning}
handle_call({connect_nodes, Ns}, From, State) ->
    ...
    case mnesia_monitor:negotiate_protocol(Check) of
	busy -> 
	    erlang:send_after(2, self(), {connect_nodes,Ns,From}),
	    {noreply, State};
	[] ->
	    gen_server:reply(From, {[], AlreadyConnected}),
	    {noreply, State};
	GoodNodes ->
	    mnesia_lib:add_list(recover_nodes, GoodNodes),
	    cast({announce_all, GoodNodes}),
	    case get_master_nodes(schema) of 
		[] ->
		    Context = starting_partitioned_network,
		    mnesia_monitor:detect_inconcistency(GoodNodes, Context);
		_ -> %% If master_nodes is set ignore old inconsistencies
		    ignore
	    end,
	    gen_server:reply(From, {GoodNodes, AlreadyConnected}),
	    {noreply,State}
    end;
...

handle_info({connect_nodes, Ns, From}, State) ->
    handle_call({connect_nodes,Ns},From,State);

%% Produces the warning:

mnesia_recover.erl:850: The inferred return type of the handle_info/2
callback includes the type {'reply','ok' | {'ok',_},_} which is not a
valid return for the gen_server behaviour

\end{code}

This was the motivation for the second part of this thesis which deals
with cases like this, where a specific call's return type is certainly
narrower than the return type of the function. See
Chapters~\ref{chp:intersection_generate}
and~\ref{chp:intersection_usage}.

\section{Use of behaviour information to find more race conditions}

\dr\ was recently extended with the ability to detect data races in
Erlang programs~\cite{Races@PADL-10}. This extension makes heavy use
of the dataflow analysis as race conditions appear when a value is
obtained from two separate processes, modified and then written back.

In cases where OTP's behaviours are used, the flow of data is
difficult to monitor because the behaviours' APIs use parameters
obtained in runtime to make calls to functions in the callback
modules. Therefore any values provided as arguments to behaviour API
calls may end up in the callback module and cause a race condition
that is impossible to detect as the call from the API to the callback
is dependent on runtime parameters. A special ``bypass'' mechanism was
added in the race detection that translates calls to the behaviour API
of OTP's behaviours into the respective call to a callback function,
as they are described in the documentation. Examples of such
translations for the \genserv\ module are given in
Table~\ref{tab:gen_server_transl}.

\begin{fulltable}{|c|c|}{OTP's \genserv\ translations}{tab:gen_server_transl}
\hline
Call to API's & ... is translated to a call to callback's\\
\hline
\hline
start\_link, start & init         \\
\hline
call, multi\_call  & handle\_call \\
\hline
cast, abcast       & handle\_cast \\
\hline
\end{fulltable}

This extension caught a specific bug that escaped the existing race
condition analysis. It is currently being tested with other additions
to the race analysis.
